class_name C_EntityFootsteps extends RayCast3D

@export var entity:CharacterBody3D 
@export var state_machine:BaseStateMachine
@export var audio_player:AudioStreamPlayer3D
@export_group("Timer Settings")
@export var walk_time:float = 0.4
@export var sprint_time:float = 0.2

var model:W_AnimatedModel3D
var timer:Timer
var rand:PseudoRandom

func _ready() -> void:
	set_process( is_multiplayer_authority() )
	set_physics_process( is_multiplayer_authority() )
	set_process_input( is_multiplayer_authority() )
	
	rand = PseudoRandom.new()
	
	if get_parent() is CharacterBody3D:
		entity = get_parent()
	
	if is_instance_valid(entity):
		SD_ECS.append_to(entity, C_EntityFootsteps)
	
	#var ecs_model:W_AnimatedModel3D = SD_ECS.find_first_component_by_script(entity, [W_AnimatedModel3D])
	#if is_instance_valid(ecs_model):
		#model = ecs_model
	
	if not is_instance_valid(audio_player):
		audio_player = AudioStreamPlayer3D.new()
		add_child(audio_player)
	
	#if not is_instance_valid(model):
	timer = Timer.new()
	
	timer.wait_time = walk_time
	add_child(timer)
	
	timer.timeout.connect(do_footstep)
	
	
	SimusNetRPC.register(
		[
			_local_play_audio
		],
		SimusNetRPCConfig.new()
			.flag_mode_any_peer()
	)

func do_footstep() -> void:
	#if not model:
	if not entity.velocity:
		return
	if state_machine:
		if timer:
			timer.wait_time = walk_time
			if state_machine.current_state.name == &"Running":
				timer.wait_time = sprint_time
		
		
		if state_machine.current_state.name == &"CrouchedWalking":
			return
	
	
	if not is_instance_valid(entity):
		return
	
	if not entity.is_on_floor():
		return
	
	var collider:Node = get_collider()
	if not is_instance_valid(collider):
		return
	
	var metadata:SD_MetadataMaterial = SD_Metadata.find_of_type(collider, SD_MetadataMaterial)
	
	if not metadata:
		return
	
	var stream:AudioStream = rand.array_pick_random(metadata.footstep_sounds)
	
	play_audio(stream)

func play_audio(stream:AudioStream) -> void:
	_local_play_audio(stream)
	SimusNetRPC.invoke(_local_play_audio, stream)

func _local_play_audio(stream:AudioStream) -> void:
	var new_player = audio_player.duplicate()
	add_child(new_player)
	
	new_player.finished.connect(new_player.queue_free)
	
	new_player.stream = stream
	new_player.play()

func _process(_delta: float) -> void:
	if not entity or not timer:
		return
	
	var speed = entity.velocity.length()
	
	if speed > 0.1 and entity.is_on_floor():
		var target_time = sprint_time if (state_machine.current_state.name == &"Running") else walk_time
		
		
		if timer.wait_time != target_time:
			timer.wait_time = target_time
			if not timer.is_stopped():
				timer.start()
		
		if timer.is_stopped():
			do_footstep()
			timer.start()
	else:
		timer.stop()
